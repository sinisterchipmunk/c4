#!/usr/bin/env coffee

{directory, template} = require 'c4/generator'

services = []
views    = []

collect = (name, dest) -> dest.push name; dest

require 'commander'
  .arguments '<module-name>',     'name of module to generate'
  .option '-s, --service [name]',
          'name of a service, can specify multiple times',
          collect, services
  .option '-v, --view [name]',
          'name of a view, can specify multiple times',
          collect, views
  .action (module_name) ->
    path = require 'path'
    directory ['app', 'modules', module_name], ->
      directory ['app', 'modules', module_name, 'services'], ->
        prefix = "app/modules/#{module_name}/services"
        for service in services
          template 'service.coffee', path.join(prefix, "#{service}.coffee"),
            service_name: service
            module_name: module_name
      directory ['app', 'modules', module_name, 'views'], ->
        prefix = "app/modules/#{module_name}/views"
        template 'module.html', path.join(prefix, 'index.html'),
          services: services
          views: views
        for view in views
          template 'view.html', path.join(prefix, "#{view}.html"),
            view_name: view
            services: services
            module_name: module_name
          template 'view.coffee', path.join(prefix, "#{view}.coffee"),
            view_name: view
            services: services
            module_name: module_name
        true
      directory ['app', 'modules', module_name, 'tests', 'views'], ->
        prefix = "app/modules/#{module_name}/tests/views"
        for view in views
          template 'view-test.coffee', path.join(prefix, "#{view}.coffee"),
            view_name: view
            module_name: module_name
      directory ['app', 'modules', module_name, 'tests', 'services'], ->
        prefix = "app/modules/#{module_name}/tests/services"
        for service in services
          template 'service-test.coffee', path.join(prefix, "#{service}.coffee"),
            service_name: service
            module_name: module_name
  .parse process.argv
